program = _{ SOI ~ TERMINATOR* ~ (stmt ~ TERMINATOR+) * ~ stmt? ~ EOI }

TERMINATOR = _{ NEWLINE | ";" }
WHITESPACE = _{ " " | "\t" }
COMMENT = _{ ("#" ~ (!NEWLINE ~ ANY)*) }

integer = @{ ASCII_DIGIT{1, 19} }
idt = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
identifier = @{ !(keyword ~ !(ASCII_ALPHANUMERIC)) ~ idt }
keyword = @{
    "if" | "else" | "while" | "do" | "end" | "debug" | "false" | "true" | "break" | "next"
}

string_literal = @{ "\"" ~ literal_char* ~ "\"" }
literal_char = { escape_sequence | (!"\"" ~ ANY) }
escape_sequence = _{ "\\\\" | "\\\"" | "\\\'" | "\\n" | "\\r" | "\\t" | "\\0" }

block = {
    (TERMINATOR* ~ (stmt ~ TERMINATOR+) * ~ stmt? ~  "end") | (":" ~ NEWLINE* ~ stmt)
}

no_else_block = {
    (TERMINATOR* ~ (stmt ~ TERMINATOR+) * ~ stmt? ~ "else") | (":"? ~ stmt ~ "else")
}

condition_if = {
    "if" ~ NEWLINE* ~ expression ~ NEWLINE* ~ block
}

condition_ifelse = {
    "if" ~ NEWLINE* ~ expression ~ NEWLINE* ~ no_else_block ~ NEWLINE* ~ NEWLINE* ~ block
}

do_block = {
    "do" ~ NEWLINE* ~ block
}

while_loop = {
    "while" ~ NEWLINE* ~ expression ~ NEWLINE* ~ block
}

indexing = {
    "[" ~ NEWLINE* ~ expression ~ NEWLINE* ~ "]"
}

suffix = {
    term ~ (indexing)+
}

false_expr = { "false" }
true_expr = { "true" }

term = _{ string_literal | integer | false_expr | true_expr | identifier | array | "(" ~ expression ~ ")" }

prefix = { prefix_operators+ ~ expression_inner }

assign = { identifier ~ "=" ~ expression }

array = { "[" ~ NEWLINE* ~ (expression ~ NEWLINE* ~ ","? ~ NEWLINE*)* ~ "]" }

// condition_ifelse must be before condition_if
expression_inner = _{ while_loop | condition_ifelse | condition_if | do_block | assign | suffix | prefix | term }

add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }
modulo = { "%" }
dbeq = { "==" }
neq = { "!=" }
gt = { ">" }
lt = { "<" }
gteq = { ">=" }
lteq = { "<=" }
bang = { "!" }
log_and = { "&&" }
log_or = { "||" }

operators = _{
    add | sub | mul | div | modulo
    | dbeq | neq | gteq | lteq | gt | lt
    | log_and | log_or
}

prefix_operators = _{
    add | sub | bang
}

infix = _{ expression_inner ~ (operators ~ expression_inner)+ }

debug_print = { "debug" ~ expression }

break_stmt = { "break" }
next_stmt = { "next" }

expression = { infix | expression_inner }

expression_stmt = { expression }

stmt = _{
    debug_print
    | break_stmt
    | next_stmt
    | expression_stmt
}